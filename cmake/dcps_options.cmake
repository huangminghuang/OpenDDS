## this file must be included from the root of DDS project
set(all_options
    BUILT_IN_TOPICS
    CONTENT_SUBSCRIPTION
    QUERY_CONDITION
    CONTENT_FILTERED_TOPIC
    MULTI_TOPIC
    OWNERSHIP_PROFILE
    OWNERSHIP_KIND_EXCLUSIVE
    OBJECT_MODEL_PROFILE
    PERSISTENCE_PROFILE
    DDS_SUPPRESS_ANYS
)

foreach(opt ${all_options})
  option(${opt} "" ON)
endforeach()

if (NOT CONTENT_SUBSCRIPTION)
  set(QUERY_CONDITION OFF)
  set(CONTENT_FILTERED_TOPIC OFF)
  set(MULTI_TOPIC OFF)
endif()

if (NOT OWNERSHIP_PROFILE)
  # Currently there is no support for exclusion of code dealing with HISTORY depth > 1
  # therefore ownership_profile is the same as ownership_kind_exclusive.
  set(OWNERSHIP_KIND_EXCLUSIVE OFF)
endif()

foreach(opt ${all_options})
  if (NOT ${opt})
    list(APPEND DCPS_COMPILE_DEFINITIONS OPENDDS_NO_${opt})
    list(APPEND TAO_BASE_IDL_FLAGS -DOPENDDS_NO_${opt})
    list(APPEND DDS_BASE_IDL_FLAGS -DOPENDDS_NO_${opt})
  endif()
endforeach()

option(NO_OPENDDS_SAFETY_PROFILE "" ON)

if (NO_OPENDDS_SAFETY_PROFILE)
  list(APPEND DCPS_OPTIONAL_LINK_LIBRARIES TAO_BiDirGIOP)
  set(DCPS_DEFAULT_DISCOVERY_LIBS OpenDDS_InfoRepoDiscovery)
else()
  list(APPEND DCPS_COMPILE_DEFINITIONS OPENDDS_SAFETY_PROFILE TAOLIB_ERROR=ACELIB_ERROR TAOLIB_DEBUG=ACELIB_DEBUG)
  list(APPEND TAO_BASE_IDL_FLAGS -DOPENDDS_SAFETY_PROFILE)
  list(APPEND DDS_BASE_IDL_FLAGS -DOPENDDS_SAFETY_PROFILE)
  set(DCPS_DEFAULT_DISCOVERY_LIBS OpenDDS_Rtps)
endif()

if (DDS_SUPPRESS_ANYS)
  list(APPEND TAO_BASE_IDL_FLAGS -Sa -St)
  list(APPEND DDS_BASE_IDL_FLAGS -Sa -St)
endif()

list(APPEND TAO_BASE_IDL_FLAGS -I${CMAKE_CURRENT_SOURCE_DIR})

list(APPEND DDS_OPTIONS NO_OPENDDS_SAFETY_PROFILE TAO_BASE_IDL_FLAGS DDS_BASE_IDL_FLAGS)

if (CONTENT_SUBSCRIPTION AND (QUERY_CONDITION OR CONTENT_FILTERED_TOPIC OR MULTI_TOPIC))
  set(CONTENT_SUBSCRIPTION_CORE TRUE)
endif()

if (NO_OPENDDS_SAFETY_PROFILE)
  set(DCPS_TRANSPORTS_FOR_TEST
    OpenDDS_Tcp OpenDDS_Udp OpenDDS_Multicast OpenDDS_Rtps_Udp
  )
  if(${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
    set(TEST_EXCLUDE_LABELS SHMEM CACHE INTERNAL "")
  else()
    list(APPEND DCPS_TRANSPORTS_FOR_TEST OpenDDS_Shmem)
  endif()
else()
  set(DCPS_TRANSPORTS_FOR_TEST OpenDDS_Rtps_Udp)
endif()


