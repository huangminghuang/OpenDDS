## this file must be included from the root of DDS project
set(OPENDDS_BASE_OPTIONS
    OPENDDS_HAS_CONTENT_SUBSCRIPTION
    OPENDDS_HAS_QUERY_CONDITION
    OPENDDS_HAS_CONTENT_FILTERED_TOPIC
    OPENDDS_HAS_MULTI_TOPIC
    OPENDDS_HAS_OWNERSHIP_PROFILE
    OPENDDS_HAS_OWNERSHIP_KIND_EXCLUSIVE
    OPENDDS_HAS_OBJECT_MODEL_PROFILE
    OPENDDS_HAS_PERSISTENCE_PROFILE
)

foreach(opt OPENDDS_HAS_BUILT_IN_TOPICS ${OPENDDS_BASE_OPTIONS})
  option(${opt} "" ON)
endforeach()

if (NOT OPENDDS_HAS_CONTENT_SUBSCRIPTION)
  set(OPENDDS_HAS_QUERY_CONDITION OFF)
  set(OPENDDS_HAS_CONTENT_FILTERED_TOPIC OFF)
  set(OPENDDS_HAS_MULTI_TOPIC OFF)
endif()

if (NOT OPENDDS_HAS_OWNERSHIP_PROFILE)
  # Currently there is no support for exclusion of code dealing with HISTORY depth > 1
  # therefore ownership_profile is the same as ownership_kind_exclusive.
  set(OPENDDS_HAS_OWNERSHIP_KIND_EXCLUSIVE OFF)
endif()

option(OPENDDS_SAFETY_PROFILE "" OFF)
option(OPENDDS_SUPPRESS_ANYS "" ON)

if (OPENDDS_HAS_CONTENT_SUBSCRIPTION AND (OPENDDS_HAS_QUERY_CONDITION OR OPENDDS_HAS_CONTENT_FILTERED_TOPIC OR OPENDDS_HAS_MULTI_TOPIC))
  set(OPENDDS_HAS_CONTENT_SUBSCRIPTION_CORE TRUE)
endif()

if (OPENDDS_SAFETY_PROFILE)
  list(APPEND DCPS_COMPILE_DEFINITIONS OPENDDS_SAFETY_PROFILE)
endif()

foreach(opt ${OPENDDS_BASE_OPTIONS})
  if (NOT ${opt})
    string(REPLACE OPENDDS_HAS OPENDDS_NO inverted_opt ${opt})
    list(APPEND DCPS_COMPILE_DEFINITIONS ${inverted_opt})
  endif()
endforeach()

if (NOT OPENDDS_HAS_BUILT_IN_TOPICS)
  list(APPEND DCPS_COMPILE_DEFINITIONS DDS_HAS_MINIMUM_BIT)
endif()

set(DDS_OPTIONS ${DCPS_COMPILE_DEFINITIONS}
                OPENDDS_HAS_BUILT_IN_TOPICS
                OPENDDS_SUPPRESS_ANYS
                OPENDDS_SAFETY_PROFILE
                OPENDDS_HAS_CONTENT_SUBSCRIPTION_CORE
                DCPS_COMPILE_DEFINITIONS
                TAO_BASE_IDL_FLAGS
                DDS_BASE_IDL_FLAGS)

